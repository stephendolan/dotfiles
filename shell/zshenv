# vim: set filetype=zsh :
# Environment for all shells (SSH commands, scripts, interactive)

# Locale
export LC_ALL=en_US.UTF-8
export LANG=en_US.UTF-8

# Editor
export VISUAL=nvim
export EDITOR=$VISUAL
export MANPAGER="nvim -c 'set ft=man' -"

# Homebrew environment
if [[ -f /opt/homebrew/bin/brew ]]; then
  eval "$(/opt/homebrew/bin/brew shellenv)"
elif [[ -f /home/linuxbrew/.linuxbrew/bin/brew ]]; then
  eval "$(/home/linuxbrew/.linuxbrew/bin/brew shellenv)"
fi

# Homebrew behavior
export HOMEBREW_NO_ANALYTICS=1
export HOMEBREW_UPGRADE_CLEANUP=1
export HOMEBREW_NO_INSTALLED_DEPENDENTS_CHECK=1
export HOMEBREW_NO_ENV_HINTS=1

typeset -U path
path=(
  "$HOME/.bun/bin"
  "$HOME/.local/bin"
  "$HOME/.docker/bin"
  $path
)

# Ripgrep config path
export RIPGREP_CONFIG_PATH=$HOME/.ripgreprc

# ASDF version manager
if [[ -f /opt/homebrew/opt/asdf/libexec/asdf.sh ]]; then
    source /opt/homebrew/opt/asdf/libexec/asdf.sh
elif [[ -f /home/linuxbrew/.linuxbrew/opt/asdf/libexec/asdf.sh ]]; then
    source /home/linuxbrew/.linuxbrew/opt/asdf/libexec/asdf.sh
fi

# FZF defaults (used by vim plugins, git tools, etc.)
export BAT_THEME="TwoDark"
export FZF_DEFAULT_COMMAND='rg --files --hidden --follow'
export FZF_COLORS="fg:-1,bg:-1,hl:230,fg+:3,bg+:233,hl+:229,info:150,prompt:110,spinner:150,pointer:167,marker:174"
export FZF_DEFAULT_OPTS="--color=$FZF_COLORS --height=40% --layout=reverse --border --bind='ctrl-/:toggle-preview'"

# Platform-specific environment
[[ -f $HOME/.zshenv.mac ]] && source $HOME/.zshenv.mac

# SSH agent configuration (unless forwarded agent is present)
# Forwarded agents have paths like /tmp/ssh-XXXXX/agent.PID
if [[ ! "$SSH_AUTH_SOCK" =~ /ssh-.*/agent ]]; then
  if [[ "$(uname)" == "Darwin" ]]; then
    __1p_sock="$HOME/Library/Group Containers/2BUA8C4S2C.com.1password/t/agent.sock"
  else
    __1p_sock="$HOME/.1password/agent.sock"
  fi
  if [[ -S "$__1p_sock" ]]; then
    export SSH_AUTH_SOCK="$__1p_sock"
  elif [[ "$(uname)" == "Linux" ]]; then
    # Fallback to standard SSH agent on Linux when 1Password unavailable
    __agent_file="$HOME/.ssh/agent-env"
    if [[ -f "$__agent_file" ]]; then
      source "$__agent_file" >/dev/null
      # Verify agent is still running
      kill -0 "$SSH_AGENT_PID" 2>/dev/null || unset SSH_AUTH_SOCK SSH_AGENT_PID
    fi
    if [[ -z "$SSH_AUTH_SOCK" ]]; then
      eval "$(ssh-agent -s)" >/dev/null
      echo "SSH_AUTH_SOCK=$SSH_AUTH_SOCK; export SSH_AUTH_SOCK;" > "$__agent_file"
      echo "SSH_AGENT_PID=$SSH_AGENT_PID; export SSH_AGENT_PID;" >> "$__agent_file"
      ssh-add 2>/dev/null
    fi
    unset __agent_file
  fi
  unset __1p_sock
fi
